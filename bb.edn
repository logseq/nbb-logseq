{:paths ["script"]
 :deps
 {nbb.build/nbb.build
  {:git/url "https://github.com/babashka/nbb"
   :git/sha "8bb7edfe0a0339fc55c9b9590b89472673c21b28"
   :deps/root "build"}
  #_{:local/root "../nbb/build"}
  nbb/nbb
  {:git/url "https://github.com/babashka/nbb"
   :git/sha "8bb7edfe0a0339fc55c9b9590b89472673c21b28"
   :git/tag "v0.7.135"}
  #_{:local/root "../nbb"}
  datascript/deps
  {:git/url "https://github.com/babashka/nbb-features"
   :git/sha "912ca86f1744b80d58d2c5c613df3a77d9f8624e"
   :deps/root "features/datascript"}
  datascript-transit/deps
  {:git/url "https://github.com/babashka/nbb-features"
   :git/sha "912ca86f1744b80d58d2c5c613df3a77d9f8624e"
   :deps/root "features/datascript-transit"}
  linked/deps
  {:git/url "https://github.com/babashka/nbb-features"
   :git/sha "912ca86f1744b80d58d2c5c613df3a77d9f8624e"
   :deps/root "features/linked"}
  cljs-time/deps {:local/root "features/cljs-time"}}

 :tasks
 {:requires ([babashka.fs :as fs]
             [nbb.build :as build]
             [clojure.string :as str])

  clean (fs/delete-tree "lib")

  npm-install (shell "npm install")

  release {:depends [clean npm-install]
           :doc "Compiles release build."
           :task (build/release *command-line-args*)}

  test nbb-feature-tests/main

  update-nbb
  {:doc "Update to latest nbb tag"
   :extra-deps {borkdude/rewrite-edn {:mvn/version "0.2.0"}}
   :requires ([borkdude.rewrite-edn :as r])
   :task
   (let [update-bb-dep
         (fn [dep dep-map]
           (let [nodes (-> "bb.edn" slurp r/parse-string)]
             (spit "bb.edn"
                   (str (reduce (fn [acc [k v]]
                                  (r/assoc-in acc [:deps dep k] v))
                                nodes
                                dep-map)))))
         [_ sha tag]
         (->> (shell {:out :string} "git ls-remote --heads --tags https://github.com/babashka/nbb.git")
              :out
              str/split-lines
              last
              (re-matches (re-pattern "(\\S+)\\trefs/tags/([v.0-9]+).*")))]
     (assert (and sha tag) "Sha and tag must exist to continue")
     (update-bb-dep 'nbb/nbb {:git/sha sha :git/tag tag})
     (update-bb-dep 'nbb.build/nbb.build {:git/sha sha})
     (println "Update nbb to" tag)
     (shell "git commit -m" (str "Update nbb to " tag) "."))}

  publish
  {:doc "Update package.json, git tag and push to CI for release"
   :task
   (let [version (or (first *command-line-args*)
                     (throw (ex-info "No version given" {})))]
     (shell "npm version" version)
     (shell "git push --atomic origin main" (str "v" version)))}

  ;; Publish tasks copied from nbb
  current-tag (->> (shell {:out :string} "git describe")
                   :out
                   str/trim
                   (re-matches (re-pattern "^v\\d+\\.\\d+\\.\\d+")))

  current-branch (->> (shell {:out :string} "git rev-parse --abbrev-ref HEAD")
                      :out
                      str/trim)

  ci:is-release {:depends [current-tag current-branch]
                 :task (and current-tag (= "main" current-branch))}

  ci:publish {:doc "Publishes release build to npm"
              :depends [ci:is-release]
              :task
              (if ci:is-release
                (do
                  (println "Releasing")
                  (run 'release)
                  (shell "npm publish --access public"))
                (println "Skipping release."))}}}
